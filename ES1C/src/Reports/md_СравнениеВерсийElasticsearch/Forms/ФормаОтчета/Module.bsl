
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
   	
   	Перем ДанныеНастроек;
   	
    ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
    ОтчетМетаданные = ОтчетОбъект.Метаданные();
    
    КлючОбъекта = ОтчетМетаданные.ПолноеИмя();
    НаименованиеОтчета = СокрЛП(ОтчетМетаданные.Представление());
    
    Параметры.Свойство("ДанныеНастроек", ДанныеНастроек);
    
    Если ЗначениеЗаполнено(ДанныеНастроек) Тогда
    	
    	ЗаполнитьНастройкиИзПараметров = Истина;
    	ПолучитьДанныеНастроекИзПараметров(ДанныеНастроек);
    	
    КонецЕсли;
    
    УстановитьЗаголовокОписанияВерсий();
    
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
       
    УстановитьОбработчикиСобытийПользовательскихНастроек();
        
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки, ИспользуютсяСтандартныеНастройки)

	ЗаполнитьПользовательскиеНастройкиИзПараметров();

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)
	
	ЗаполнитьПользовательскиеНастройкиИзПараметров();
	
КонецПроцедуры

Процедура ЗаполнитьПользовательскиеНастройкиИзПараметров()
	
	Если ЗаполнитьНастройкиИзПараметров Тогда
		
		КоллекцияНастроек = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
		УстановитьПользовательскиеНастройкиИзПараметров(КоллекцияНастроек);	
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийШапкиФормы

&НаКлиенте
Процедура Подключаемый_СсылкаНаОбъект_ПриИзменении(Элемент)
    
    ЗаполнитьСводныеДанныеОбъекта();
    КоллекцияНастроек = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
    УстановитьЗначениеПользовательскогоПараметраПоУмолчанию(КоллекцияНастроек, "КоличествоВерсий", ВсегоВерсий);
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
    
    СкомпоноватьРезультат(РежимКомпоновкиРезультата.Фоновый);
    
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции


&НаСервере
Процедура УстановитьЗаголовокОписанияВерсий()
	
	Элементы.ВсегоВерсийДекорация.Заголовок = md_СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	"Всего версий объекта: %1", ВсегоВерсий);
	
КонецПроцедуры 

&НаСервере
Процедура ПолучитьДанныеНастроекИзПараметров(ДанныеНастроек)

	ДанныеНастроек.Свойство("ДокументСсылка", ВерсионируемыйОбъект);
	ДанныеНастроек.Свойство("ДатаОтбора", ДатаОтбора);
	ДанныеНастроек.Свойство("ИндексES", ИндексES);
	ДанныеНастроек.Свойство("СерверES", СерверES);
	ДанныеНастроек.Свойство("ВсегоВерсий", ВсегоВерсий);

КонецПроцедуры

&НаСервере
Процедура УстановитьПользовательскиеНастройкиИзПараметров(КоллекцияНастроек)
   
    УстановитьЗначениеПользовательскогоПараметраПоУмолчанию(КоллекцияНастроек, "СсылкаНаОбъект", ВерсионируемыйОбъект);
    УстановитьЗначениеПользовательскогоПараметраПоУмолчанию(КоллекцияНастроек, "ДатаОтбора", ДатаОтбора);
    УстановитьЗначениеПользовательскогоПараметраПоУмолчанию(КоллекцияНастроек, "ИндексES", ИндексES);
    УстановитьЗначениеПользовательскогоПараметраПоУмолчанию(КоллекцияНастроек, "СерверES", СерверES);
    УстановитьЗначениеПользовательскогоПараметраПоУмолчанию(КоллекцияНастроек, "КоличествоВерсий", ВсегоВерсий);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗначениеПользовательскогоПараметраПоУмолчанию(КоллекцияНастроек, ИмяПараметра, Значение)
    
    Для Каждого ПользовательскаяНастройка Из КоллекцияНастроек Цикл
        
        Если ТипЗнч(ПользовательскаяНастройка) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных")  
            И ПользовательскаяНастройка.Параметр = Новый ПараметрКомпоновкиДанных(ИмяПараметра) Тогда
            
            ПользовательскаяНастройка.Значение = Значение;
            ПользовательскаяНастройка.Использование = Истина;
            
        КонецЕсли;
        
    КонецЦикла;  
    
КонецПроцедуры

&НаКлиенте
Функция ПолучитьЗначениеПараметра(Параметры, ИмяПараметра)
    
    Для Каждого ПользовательскаяНастройка Из Параметры Цикл
        
        Если ТипЗнч(ПользовательскаяНастройка) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных")  
            И ПользовательскаяНастройка.Параметр = Новый ПараметрКомпоновкиДанных(ИмяПараметра) Тогда
            
            Возврат ПользовательскаяНастройка.Значение;
            
        КонецЕсли;
        
    КонецЦикла; 
    
    Возврат Неопределено;
    
КонецФункции

&НаКлиенте
Процедура ЗаполнитьСводныеДанныеОбъекта()
    
    ВсегоВерсий = 0;
	ВсеПараметры = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
		
	ВерсионируемыйОбъект = ПолучитьЗначениеПараметра(ВсеПараметры, "СсылкаНаОбъект");
	СерверES = ПолучитьЗначениеПараметра(ВсеПараметры, "СерверES");
	ИндексES = ПолучитьЗначениеПараметра(ВсеПараметры, "ИндексES");

    Если ЗначениеЗаполнено(ВерсионируемыйОбъект) И ЗначениеЗаполнено(СерверES) 
        И ЗначениеЗаполнено(ИндексES) Тогда
        
        СводныеДанные = ПолучитьСводныеДанныеОбъектаНаСервере();	
        СводныеДанные.Свойство("ВсегоВерсий", ВсегоВерсий);  
        
    КонецЕсли; 
    
    Элементы.ВсегоВерсийДекорация.Заголовок = md_СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
    "Всего версий объекта: %1", ВсегоВерсий);
    
КонецПроцедуры

&НаСервере
Функция ПолучитьСводныеДанныеОбъектаНаСервере()
    
    Возврат md_ВерсионированиеElasticsearchСлужебный.ПолучитьСводныеДанныеОбъекта(
    СерверES,
    ИндексES, 
    ВерсионируемыйОбъект);
    
КонецФункции

&НаСервере
Процедура УстановитьОбработчикиСобытийПользовательскихНастроек()
    
    Для Каждого ЭлементФормы Из ЭтотОбъект.Элементы Цикл
        
        Если ТипЗнч(ЭлементФормы) = Тип("ГруппаФормы") И ЭлементФормы.Заголовок = "Ссылка на объект" Тогда
            
            Если НЕ ЭлементФормы.ПодчиненныеЭлементы.Количество() = 0 Тогда
                
                Для Каждого ПодчиненныйЭлемент Из ЭлементФормы.ПодчиненныеЭлементы Цикл
                    
                    Если ТипЗнч(ПодчиненныйЭлемент) = Тип("ПолеФормы") Тогда
                        
                        ПодчиненныйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_СсылкаНаОбъект_ПриИзменении")
                        
                    КонецЕсли; 
                    
                КонецЦикла; 
                
            КонецЕсли;
            
        КонецЕсли; 
        
    КонецЦикла; 
    
КонецПроцедуры

#КонецОбласти 


