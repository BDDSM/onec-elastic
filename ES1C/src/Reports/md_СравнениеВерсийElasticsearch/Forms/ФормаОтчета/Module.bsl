
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
    ОтчетМетаданные = ОтчетОбъект.Метаданные();
    
    КлючОбъекта = ОтчетМетаданные.ПолноеИмя();
    НаименованиеОтчета = СокрЛП(ОтчетМетаданные.Представление());
    
    Параметры.Свойство("ВариантОтчетаПоУмолчанию", ВариантОтчетаПоУмолчанию);
    Параметры.Свойство("ДокументСсылка", ВерсионируемыйОбъект);
    
    СводныеДанныеПоВерсиямПолучены = Ложь;
    
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)
    
    Если ЗначениеЗаполнено(ВариантОтчетаПоУмолчанию) Тогда
        
        НастройкиВарианта = md_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВариантОтчетаПоУмолчанию, "Настройки");
        НастройкиВариантаДанные = НастройкиВарианта.Получить();
        Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиВариантаДанные);
        
        Если ЗначениеЗаполнено(ВерсионируемыйОбъект) Тогда
            
            УстановитьПользовательскиеНастройкиПоУмолчанию();
            
        КонецЕсли;
        
    КонецЕсли; 
    
КонецПроцедуры

&НаСервере
Процедура УстановитьПользовательскиеНастройкиПоУмолчанию(НастройкиСКД = Неопределено)
    
    Если НастройкиСКД = Неопределено Тогда
        
        КоллекцияНастроек = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
        
    Иначе
        
        КоллекцияНастроек = НастройкиСКД.Элементы;
        
    КонецЕсли; 
    
    УстановитьЗначениеПользовательскогоПараметраПоУмолчанию(КоллекцияНастроек, "СсылкаНаОбъект", 
    ВерсионируемыйОбъект);
    
    ПараметрДатаОтбора = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(
    Новый ПараметрКомпоновкиДанных("ДатаОтбора"));
    
    ДатаОтбора = КонецДня(ТекущаяДатаСеанса())+1;
   
    Если НЕ ПараметрДатаОтбора = Неопределено Тогда
        
        Если ТипЗнч(ПараметрДатаОтбора.Значение) = Тип("СтандартнаяДатаНачала") Тогда
            
            ДатаОтбора = ПараметрДатаОтбора.Значение.Дата;
            
        Иначе
            
            ДатаОтбора = ПараметрДатаОтбора.Значение;
            
        КонецЕсли; 
           
	КонецЕсли;
	
    УстановитьЗначениеПользовательскогоПараметраПоУмолчанию(КоллекцияНастроек, "ДатаОтбора", ДатаОтбора);

    ПараметрСерверElasticsearch = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(
    Новый ПараметрКомпоновкиДанных("СерверElasticsearch"));
    
    Если НЕ ПараметрСерверElasticsearch = Неопределено Тогда
        
        СерверElasticsearch = ПараметрСерверElasticsearch.Значение;
        
    КонецЕсли; 
    
    ПараметрИндексES = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(
    Новый ПараметрКомпоновкиДанных("ИндексES"));
    
    Если НЕ ПараметрИндексES = Неопределено Тогда
        
        ИндексES = ПараметрИндексES.Значение;
        
    КонецЕсли; 
    
    ВсегоВерсий = 0;
    
    СводныеДанные = ПолучитьСводныеДанныеОбъектаНаСервере();	
    
    Если СводныеДанные.Свойство("ЕстьОшибка") Тогда
        
        ВызватьИсключение СводныеДанные.ТелоОтвета;
        
    КонецЕсли; 
    
    СводныеДанные.Свойство("ВсегоВерсий", ВсегоВерсий); 	
    Элементы.ВсегоВерсийДекорация.Заголовок = md_СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
    "Всего версий объекта: %1", ВсегоВерсий);
    
    УстановитьЗначениеПользовательскогоПараметраПоУмолчанию(КоллекцияНастроек, "КоличествоВерсий", ВсегоВерсий);
    СводныеДанныеПоВерсиямПолучены = Истина;
    
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    
    УстановитьОбработчикиСобытийПользовательскихНастроек();
    
    Если НЕ СводныеДанныеПоВерсиямПолучены Тогда
        
        ПолучитьКоличествоВерсийОбъекта();
        
    КонецЕсли; 
    
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СсылкаНаОбъект_ПриИзменении(Элемент)
    
    ПолучитьКоличествоВерсийОбъекта();
    КоллекцияНастроек = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы;
    УстановитьЗначениеПользовательскогоПараметраПоУмолчанию(КоллекцияНастроек, "КоличествоВерсий", ВсегоВерсий);
    
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
    
    СуффиксФормыИсточника = ИсточникВыбора.ИмяФормы;
    ПозицияТочки = СтрДлина(СуффиксФормыИсточника);
    Пока Сред(СуффиксФормыИсточника, ПозицияТочки, 1) <> "." Цикл
        ПозицияТочки = ПозицияТочки - 1;
    КонецЦикла;
    СуффиксФормыИсточника = ВРег(Сред(СуффиксФормыИсточника, ПозицияТочки + 1));
    
    Если СуффиксФормыИсточника = ВРег("ФормаВариантаОтчета")
        ИЛИ СуффиксФормыИсточника = ВРег("ФормаВарианта") Тогда
        ИзмененияИзФормыВариантаОтчета = Истина;
    КонецЕсли;
    
    Если ИзмененияИзФормыВариантаОтчета <> Неопределено Тогда
        
        ЗагрузитьСхемуИОчиститьФорму(ВыбранноеЗначение);
        
    КонецЕсли;
    
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСхемуИОчиститьФорму(НовыеНастройки)
    
    Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(НовыеНастройки.Настройки);
    Отчет.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(НовыеНастройки.ПользовательскиеНастройки);
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
    
    СкомпоноватьРезультат(РежимКомпоновкиРезультата.Фоновый);
    
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВариантОтчета(Команда)
    
    ПараметрыФормы = Новый Структура;
    
    ПараметрыФормы.Вставить("Вариант",                               Отчет.КомпоновщикНастроек.Настройки);
    ПараметрыФормы.Вставить("КлючВарианта",                          Строка(КлючТекущегоВарианта));
    ПараметрыФормы.Вставить("ПользовательскиеНастройки",             Отчет.КомпоновщикНастроек.ПользовательскиеНастройки);
    ПараметрыФормы.Вставить("ПредставлениеВарианта",                 Строка(КлючТекущегоВарианта));
    ПараметрыФормы.Вставить("ПредставлениеПользовательскихНастроек", "");
    
    ОткрытьФорму(КлючОбъекта + ".ФормаВарианта", ПараметрыФормы, ЭтотОбъект);
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПолучитьКоличествоВерсийОбъекта()
    
    ПолучитьВерсионируемыйОбъектКонтроля();
    
    ПолучитьПараметрыИнстансаElasticsearch(
    Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы);
    
    ЗаполнитьСводныеДанныеОбъекта();
    
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗначениеПользовательскогоПараметраПоУмолчанию(КоллекцияНастроек, ИмяПараметра, Значение)
    
    Для Каждого ПользовательскаяНастройка Из КоллекцияНастроек Цикл
        
        Если ТипЗнч(ПользовательскаяНастройка) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных")  
            И ПользовательскаяНастройка.Параметр = Новый ПараметрКомпоновкиДанных(ИмяПараметра) Тогда
            
            ПользовательскаяНастройка.Значение = Значение;
            ПользовательскаяНастройка.Использование = Истина;
            
        КонецЕсли;
        
    КонецЦикла;  
    
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСводныеДанныеОбъекта()
    
    ВсегоВерсий = 0;
    
    Если ЗначениеЗаполнено(ВерсионируемыйОбъект) И ЗначениеЗаполнено(СерверElasticsearch) 
        И ЗначениеЗаполнено(ИндексES) Тогда
        
        СводныеДанные = ПолучитьСводныеДанныеОбъектаНаСервере();	
        СводныеДанные.Свойство("ВсегоВерсий", ВсегоВерсий);  
        
    КонецЕсли; 
    
    Элементы.ВсегоВерсийДекорация.Заголовок = md_СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
    "Всего версий объекта: %1", ВсегоВерсий);
    
КонецПроцедуры

&НаСервере
Функция ПолучитьСводныеДанныеОбъектаНаСервере()
    
    Возврат md_ВерсионированиеElasticsearchСлужебный.ПолучитьСводныеДанныеОбъекта(
    СерверElasticsearch,
    ИндексES, 
    ВерсионируемыйОбъект);
    
КонецФункции

&НаКлиенте
Процедура ПолучитьЗначениеПараметра(ИмяРевизитаФормы, ИмяПараметра, КоллекцияЭлементовНастроек)
    
    Для Каждого ПользовательскаяНастройка Из КоллекцияЭлементовНастроек Цикл
        
        Если ТипЗнч(ПользовательскаяНастройка) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных")  
            И ПользовательскаяНастройка.Параметр = Новый ПараметрКомпоновкиДанных(ИмяПараметра) Тогда
            
            ЭтотОбъект[ИмяРевизитаФормы] = ПользовательскаяНастройка.Значение;
            
        КонецЕсли;
        
    КонецЦикла; 
    
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВерсионируемыйОбъектКонтроля()
    
    ПолучитьЗначениеПараметра("ВерсионируемыйОбъект", "СсылкаНаОбъект",
    Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы);
    
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьПараметрыИнстансаElasticsearch(КоллекцияНастроек)
    
    ПолучитьЗначениеПараметра("СерверElasticsearch", "СерверElasticsearch", КоллекцияНастроек);
    
    ПолучитьЗначениеПараметра("ИндексES", "ИндексES", КоллекцияНастроек);
    
КонецПроцедуры 

&НаСервере
Процедура УстановитьОбработчикиСобытийПользовательскихНастроек()
    
    Для Каждого ЭлементФормы Из ЭтотОбъект.Элементы Цикл
        
        Если ТипЗнч(ЭлементФормы) = Тип("ГруппаФормы") И ЭлементФормы.Заголовок = "Ссылка на объект" Тогда
            
            Если НЕ ЭлементФормы.ПодчиненныеЭлементы.Количество() = 0 Тогда
                
                Для Каждого ПодчиненныйЭлемент Из ЭлементФормы.ПодчиненныеЭлементы Цикл
                    
                    Если ТипЗнч(ПодчиненныйЭлемент) = Тип("ПолеФормы") Тогда
                        
                        ПодчиненныйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_СсылкаНаОбъект_ПриИзменении")
                        
                    КонецЕсли; 
                    
                КонецЦикла; 
                
            КонецЕсли;
            
        КонецЕсли; 
        
    КонецЦикла; 
    
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойПользовательскихНастроекНаСервере(Настройки)
    
    Если ЗначениеЗаполнено(ВерсионируемыйОбъект) Тогда
            
        УстановитьПользовательскиеНастройкиПоУмолчанию(Настройки);
            
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти 


