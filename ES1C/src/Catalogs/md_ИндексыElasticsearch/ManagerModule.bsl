#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Перем Нода, СсылкаНаОбъект;
	
	Параметры.Свойство("Нода", Нода);
	Параметры.Свойство("СсылкаНаОбъект", СсылкаНаОбъект);
	
	Если ЗначениеЗаполнено(Нода) И ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	md_НастройкаВерсионированияОбъектовВElasticsearch.ИндексES КАК Индекс
		|ИЗ
		|	РегистрСведений.md_НастройкаВерсионированияОбъектовВElasticsearch КАК
		|		md_НастройкаВерсионированияОбъектовВElasticsearch
		|ГДЕ
		|	md_НастройкаВерсионированияОбъектовВElasticsearch.ТипМетаданных = &ТипМетаданных
		|	И md_НастройкаВерсионированияОбъектовВElasticsearch.СерверElasticsearch = &СерверElasticsearch";
		
		Запрос.УстановитьПараметр("ТипМетаданных", СсылкаНаОбъект.Метаданные().ПолноеИмя());
		Запрос.УстановитьПараметр("СерверElasticsearch", Нода);
		
		Индексы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Индекс");
		
		ДанныеВыбора = Новый СписокЗначений();
		ДанныеВыбора.ЗагрузитьЗначения(Индексы);
		
		СтандартнаяОбработка = Ложь;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти