
#Область ОбработчикиСобытий 

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если НЕ ЗначениеЗаполнено(ПараметрКоманды) Тогда
		
		ВызватьИсключение "Не выбран объект версионирования";
			
	КонецЕсли;
	
	ПараметрыВерсионирования = ПараметрыВерсионирования(ПараметрКоманды);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПараметрыВерсионирования", ПараметрыВерсионирования);
	ПараметрыФормы.Вставить("СформироватьПриОткрытии", Истина);
	
	ОткрытьФорму("Отчет.md_СравнениеВерсийElasticsearch.Форма",
	ПараметрыФормы,
	ПараметрыВыполненияКоманды.Источник, 
	ПараметрыВыполненияКоманды.Уникальность, 
	ПараметрыВыполненияКоманды.Окно, 
	ПараметрыВыполненияКоманды.НавигационнаяСсылка);
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаСервере
Функция ПараметрыВерсионирования(СсылкаНаОбъект)
	
	ВсегоВерсий = 0;
	ПараметрыВерсионирования = Новый Структура("СерверES, ИндексES");
	
	ДанныеИнстанса = ПолучитьДанныеИнстансаОбъекта(СсылкаНаОбъект);
	ЗаполнитьЗначенияСвойств(ПараметрыВерсионирования, ДанныеИнстанса);
	
	ПараметрыВерсионирования.Вставить("ДокументСсылка", СсылкаНаОбъект);
	ПараметрыВерсионирования.Вставить("ДатаОтбора", ТекущаяУниверсальнаяДата());
	
    СводныеДанные = md_ВерсионированиеElasticsearchСлужебный.ПолучитьСводныеДанныеОбъекта(
    ДанныеИнстанса.СерверES,
    ДанныеИнстанса.ИндексES, 
    СсылкаНаОбъект);
    
	СводныеДанные.Свойство("ВсегоВерсий", ВсегоВерсий);  
	
	ПараметрыВерсионирования.Вставить("ВсегоВерсий", ВсегоВерсий);
	
	Возврат ПараметрыВерсионирования;
	
КонецФункции

&НаСервере
Функция ПолучитьДанныеИнстансаОбъекта(СсылкаНаОбъект)
	
	ДанныеИнстанса = Новый Структура("СерверES, ИндексES");
	МетаданныеОбъекта = СсылкаНаОбъект.Метаданные();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	md_НастройкаВерсионированияОбъектовВElasticsearch.СерверElasticsearch КАК СерверES,
		|	md_НастройкаВерсионированияОбъектовВElasticsearch.ИндексES КАК ИндексES
		|ИЗ
		|	РегистрСведений.md_НастройкаВерсионированияОбъектовВElasticsearch КАК
		|		md_НастройкаВерсионированияОбъектовВElasticsearch
		|ГДЕ
		|	md_НастройкаВерсионированияОбъектовВElasticsearch.Использование
		|	И md_НастройкаВерсионированияОбъектовВElasticsearch.ТипМетаданных = &ТипМетаданных
		|	И
		|	НЕ md_НастройкаВерсионированияОбъектовВElasticsearch.Приостановлено";
	
	Запрос.УстановитьПараметр("ТипМетаданных", МетаданныеОбъекта.ПолноеИмя());
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(ДанныеИнстанса, Выборка);
		
	КонецЕсли;
	
	Возврат ДанныеИнстанса;
	
КонецФункции

#КонецОбласти





